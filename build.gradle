buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id 'net.researchgate.release' version '2.1.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'
}

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'idea'

apply from: 'libraries.gradle'
apply from: 'gradle/license.gradle'
apply from: 'gradle/release.gradle'

apply plugin: 'findbugs'
apply plugin: 'pmd'

apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8

group = "io.jmnarloch"
archivesBaseName="cassandra-kafka-log"

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'cassandra-kafka-log',
                   'Implementation-Version': version
    }
}

jar.doFirst {
    from configurations.repackage.filter({it.isFile()}).collect { zipTree(it) }
}

repositories {
    jcenter()
}

compileJava {
    options.fork = false
}

configurations {
    repackage
}

sourceSets.all {
    compileClasspath += configurations.repackage
}

javadoc {
    classpath += configurations.repackage
}

dependencies {

    compile (libraries.cassandra)
    repackage (libraries.kafkaClients)

    testCompile (libraries.junit)
    testCompile (libraries.mockito)
}

findbugs {
    ignoreFailures = true
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
